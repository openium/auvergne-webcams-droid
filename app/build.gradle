apply plugin: "com.android.application"
apply plugin: "com.google.gms.google-services"
apply plugin: "com.google.firebase.crashlytics"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"
apply plugin: "com.google.android.libraries.mapsplatform.secrets-gradle-plugin"
apply plugin: "fr.openium.version"

def keystorePropertiesFile = rootProject.file("private/keys/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdk 34

    defaultConfig {
        applicationId "fr.openium.auvergnewebcams"
        minSdkVersion 21
        targetSdkVersion 34

        versionCode openiumVersion.versionCode
        versionName openiumVersion.versionName

        ndk {
            abiFilters "arm64-v8a", "armeabi-v7a", "x86", "x86_64"
        }
    }

    signingConfigs {
        debug {
            storeFile file(keystoreProperties["debugStoreFile"])
        }
        release {
            storeFile file(keystoreProperties["releaseStoreFile"])
            storePassword keystoreProperties["passwordProd"]
            keyAlias keystoreProperties["aliasProd"]
            keyPassword keystoreProperties["passwordProd"]
        }
    }

    buildTypes {
        debug {
            minifyEnabled false

            versionNameSuffix "-debug"
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources true

            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
        }
    }

    //Export room schema for easy migration
    kapt {
        arguments {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.3.2"
    }
}

kapt { useBuildCache = true }

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar", "*.aar"])

    // Kotlin
    implementation libs.kotlin.stdlib

    // AndroidX
    implementation libs.androidx.appcompat
    implementation libs.androidx.core
    implementation libs.material
    implementation libs.work.runtime.ktx
    implementation libs.preference.ktx
    implementation libs.constraintlayout

    // Lifecycle
    implementation libs.lifecycle.extensions
    implementation libs.lifecycle.viewmodel.ktx
    implementation libs.lifecycle.common.java8

    // Firebase
    implementation platform(libs.firebase.bom)
    implementation libs.firebase.crashlytics
    implementation libs.firebase.analytics

    // Compose
    implementation platform(libs.compose.bom)
    implementation libs.compose.material
    implementation libs.compose.ui
    implementation libs.compose.ui.util
    implementation libs.compose.ui.tooling.preview
    debugImplementation libs.compose.ui.tooling
    implementation libs.compose.runtime.livedata

    // Accompanist
    implementation libs.accompanist.pager
    implementation libs.accompanist.swiperefresh
    implementation libs.accompanist.permissions

    // Openium Tools
    implementation libs.openium.tools
    implementation libs.openium.rx.tools

    // Koin
    implementation libs.koin

    // RxJava and related libraries
    implementation libs.rxjava
    implementation libs.rxbinding
    implementation libs.rxbinding.appcompat
    implementation libs.rxrelay
    implementation libs.rxpermissions

    // Retrofit and OkHttp
    implementation libs.retrofit
    implementation libs.converter.gson
    implementation libs.adapter.rxjava2
    implementation libs.okhttp.logging.interceptor
    debugImplementation libs.retrofit.mock

    // Maps
    implementation libs.maps

    // Room
    implementation libs.room.runtime
    implementation libs.room.ktx
    kapt libs.room.compiler
    implementation libs.room.rxjava2

    // Coil
    implementation libs.coil
    implementation libs.coil.video
    implementation libs.coil.compose

    // Big Image Viewer
    implementation libs.big.image.viewer

    // Coroutines
    implementation libs.coroutines.core
    implementation libs.coroutines.android

    // Logging
    implementation libs.timber

    // Testing
    testImplementation libs.junit
    androidTestImplementation libs.test.core
    androidTestImplementation libs.test.core.ktx
    androidTestImplementation libs.espresso.core
    androidTestImplementation libs.test.junit

    // ExoPlayer
    implementation libs.exoplayer

    // Like Button (Twitter)
    implementation libs.like.button

    // Mapbox
    implementation libs.mapbox
    implementation libs.mapbox.compose
}

secrets {
    propertiesFileName = "private/keys/secrets.properties"
}